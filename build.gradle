apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'docker'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.eriwen:gradle-js-plugin:1.12.1'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

apply plugin: 'com.eriwen.gradle.js'

mainClassName = 'github.frodeaa.bottle.App'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = 'bottle'
    version = '0.1.0'
}

dependencies {
    compile 'com.bladejava:blade-core:1.6.1M1'
    compile 'com.bladejava:blade-startup:1.0.1'
    compile 'com.bladejava:blade-kit:1.2.9'
    compile 'org.sql2o:sql2o:1.5.4'
    compile 'org.postgresql:postgresql:9.4-1206-jdbc42'
    compile 'org.flywaydb:flyway-core:3.2.1'
    compile 'de.svenkubiak:jBCrypt:0.4'
}

docker {
    baseImage 'frolvlad/alpine-oraclejdk8'
    maintainer 'Frode Aannevik "frode.aa@gmail.com"'
}

javascript.source {
    dev {
        js {
            srcDir file("${projectDir}/src/main/chrome-extension")
            include "*.js"
            exclude "lib/*.js"
        }
    }
}

jshint {
    tasks.jshint.source = javascript.source.dev.js.files
    tasks.jshint.dest = file("${buildDir}/jshint.out")
    tasks.jshint.outputToStdOut = false
    tasks.jshint.reporter = 'checkstyle'
    jshint.options = [
            expr   : 'true',
            unused : 'true',
            browser: 'true',
            devel  : 'true'
    ]
    jshint.predef = [
            'chrome': false
    ]

}

task bottleDocker(type: Docker, dependsOn: installDist) {
    applicationName = jar.baseName
    tagVersion = jar.version
    defaultCommand(['-cp', "lib/${applicationName}-${tagVersion}.jar:/lib/*", mainClassName])
    entryPoint(['java'])
    exposePort(9000)
    addFile {
        from "${buildDir}/install/${applicationName}/"
    }
}

